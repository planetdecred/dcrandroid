/*
 * Copyright (c) 2018-2021 The Decred developers
 * Use of this source code is governed by an ISC
 * license that can be found in the LICENSE file.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.decred.dcrandroid"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 20
        versionName "v1.6.0"
        buildConfigField("String", "BuildDate", "\"${getDate()}\"")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {

        }
    }

    flavorDimensions "nettype"
    productFlavors {
        tstnet {
            dimension "nettype"
            applicationIdSuffix ".testnet"
            buildConfigField("boolean", "IS_TESTNET", "true")
            buildConfigField("int", "TicketMaturity", "16")
            buildConfigField("int", "TargetTimePerBlock", "120")
            buildConfigField("String", "LogDir", "\"/wallets/testnet3/dcrlibwallet.log\"")
            buildConfigField("String", "NetType", "\"testnet3\"")
            resValue "string", "app_name", "Decred Wallet Testnet"
            manifestPlaceholders = [
                    appIcon     : "@mipmap/ic_launcher_testnet",
                    appIconRound: "@mipmap/ic_launcher_round_testnet"
            ]
        }

        mainnet {
            dimension "nettype"
            applicationIdSuffix ".mainnet"
            buildConfigField("boolean", "IS_TESTNET", "false")
            buildConfigField("int", "TicketMaturity", "256")
            buildConfigField("int", "TargetTimePerBlock", "300")
            buildConfigField("String", "LogDir", "\"/wallets/mainnet/dcrlibwallet.log\"")
            buildConfigField("String", "NetType", "\"mainnet\"")
            resValue "string", "app_name", "Decred Wallet"
            manifestPlaceholders = [
                    appIcon     : "@mipmap/ic_launcher_mainnet",
                    appIconRound: "@mipmap/ic_launcher_round_mainnet"
            ]
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    // Kotlin Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'

    // Android support library
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'com.google.android.material:material:1.4.0-alpha02'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.core:core:1.3.2'

    // QR code
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'

    // Crash report sender
    implementation 'ch.acra:acra-core:5.7.0'
    implementation 'ch.acra:acra-dialog:5.7.0'
    implementation 'ch.acra:acra-http:5.7.0'

    // JSON serialization & deserialization
    implementation 'com.google.code.gson:gson:2.8.6'

    // Rich Text Formatting (No WebView)
    implementation "io.noties.markwon:core:4.6.2"
    implementation "io.noties.markwon:ext-tables:4.6.2"
    implementation "io.noties.markwon:linkify:4.6.2"

    androidTestImplementation 'androidx.test:rules:1.4.0-alpha06'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0-alpha06'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

def getDate() {
    return new Date().format('yyyy-MM-dd')
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
