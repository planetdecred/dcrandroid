/*
 * Copyright (c) 2018-2019 The Decred developers
 * Use of this source code is governed by an ISC
 * license that can be found in the LICENSE file.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.decred.dcrandroid"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 3
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {

        }
    }

    flavorDimensions "nettype"
    productFlavors {
        tstnet {
            dimension "nettype"
            versionName "testnet ${getDate()}"
            applicationIdSuffix ".testnet"
            buildConfigField("boolean", "IS_TESTNET", "true")
            buildConfigField("int", "TicketMaturity", "16")
            buildConfigField("int", "TargetTimePerBlock", "120")
            buildConfigField("long", "GenesisTimestamp", "1533513600")
            buildConfigField("String", "LogDir", "\"/wallet/testnet3/dcrlibwallet.log\"")
            buildConfigField("String", "NetType", "\"testnet3\"")
            resValue "string", "app_name", "Decred Wallet Testnet"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_testnet",
                    appIconRound: "@mipmap/ic_launcher_round_testnet"
            ]
        }

        mainnet {
            dimension "nettype"
            versionName "mainnet ${getDate()}"
            applicationIdSuffix ".mainnet"
            buildConfigField("boolean", "IS_TESTNET", "false")
            buildConfigField("int", "TicketMaturity", "256")
            buildConfigField("int", "TargetTimePerBlock", "300")
            buildConfigField("long", "GenesisTimestamp", "1454954400")
            buildConfigField("String", "LogDir", "\"/wallet/mainnet/dcrlibwallet.log\"")
            buildConfigField("String", "NetType", "\"mainnet\"")
            resValue "string", "app_name", "Decred Wallet"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_mainnet",
                    appIconRound: "@mipmap/ic_launcher_round_mainnet"
            ]
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    // Android support library
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.preference:preference:1.1.0-alpha01'
    implementation 'androidx.core:core:1.0.1'

    // QR code scanner
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'

    // QR code generator
    implementation 'com.github.kenglxn.QRGen:android:2.5.0'

    // Crash report sender
    implementation 'ch.acra:acra:4.11', {
        exclude group: 'com.android.support'
    }

    // JSON serialization & deserialization
    implementation 'com.google.code.gson:gson:2.8.2'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

def getDate() {
    return new Date().format('yyyy-MM-dd')
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
